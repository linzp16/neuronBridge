#include "../source_file_realtime_v1_async/NeuralModel/inc/TimeDrivenGPU/EDLUTLikeLIF_GPU_Interface.cuh"
#include "../source_file_realtime_v1_async/NeuralModel/inc/NeuralStateVector/Neuron_State_Vector_Interface.cuh"
#include "../source_file_realtime_v1_async/NeuralModel/inc/TimeDrivenGPU/EDLUTLikeLIF_GPU.cuh"
#include "../source_file_realtime_v1_async/connections/inc/Interconnections.h"
#include "../source_file_realtime_v1_async/Current/inc/CurrentSynapse.h"
#include "../source_file_realtime_v1_async/Simulation/inc/Simulation.h"
#include "../source_file_realtime_v1_async/error/cudaerror.h"
#include "../source_file_realtime_v1_async/Openmp/inc/Openmp.h"
#include "../source_file_realtime_v1_async/Intergration/inc/GPU_Method/IntegrationMethodGPU_Interface.cuh"
#include "../source_file_realtime_v1_async/ModelFactory/Integration_method_GPU_Interface_Facory.cuh"
#include "../source_file_realtime_v1_async/mainprogram/inc/BenchProfiling.h"
#include <vector>

EDLUTLikeLIF_GPU_Interface::EDLUTLikeLIF_GPU_Interface(int timestep)
    : TimeDrivenNeuronModelGPU_Interface(timestep), CurrentSynapeModel(0), NeuronModelOnGPU(0) {
    this->StateVector = new Neuron_State_Vector_Interface(N_NeuronStateVariables);
    this->setModelName("EDLUTLikeLIF_GPU");
}

EDLUTLikeLIF_GPU_Interface::~EDLUTLikeLIF_GPU_Interface() {
    if (this->CurrentSynapeModel != 0) {
        delete this->CurrentSynapeModel;
        this->CurrentSynapeModel = 0;
    }
    this->DestroyGPUModel();
}

__global__ void DeleteEDLUTLikeClassGPU(EDLUTLikeLIF_GPU** GPU_NeuralModel) {
    if (blockIdx.x == 0 && threadIdx.x == 0) {
        delete (*GPU_NeuralModel);
    }
}

void EDLUTLikeLIF_GPU_Interface::DestroyGPUModel() {
    if (this->NeuronModelOnGPU != 0) {
        DeleteEDLUTLikeClassGPU<<<1, 1>>>(this->NeuronModelOnGPU);
        cudaFree(this->NeuronModelOnGPU);
    }
}

Neuron_State_Vector* EDLUTLikeLIF_GPU_Interface::InitState() {
    return this->StateVector;
}

InternalSpike* EDLUTLikeLIF_GPU_Interface::ProcessSpike(Interconnections* inter, int time) {
    if (inter->type == 0 && this->Excited) {
        this->State_GPU->AuxStateCPU[0 * this->State_GPU->NumberofNeuron + inter->TargetNeuronModelIndex] += inter->weight;
    } else if (inter->type == 1 && this->Inhibitory) {
        this->State_GPU->AuxStateCPU[1 * this->State_GPU->NumberofNeuron + inter->TargetNeuronModelIndex] += inter->weight;
    }
    return 0;
}

void EDLUTLikeLIF_GPU_Interface::ProcessCurrent(Interconnections* inter, Neuron* Target, float current) {
    this->CurrentSynapeModel->SetInputCurrentPerSynapse(Target->index_in_NeuronModel, inter->subindex_type, current);
    float total_current = this->CurrentSynapeModel->GetTotalInputCurrentPerNeuron(Target->index_in_NeuronModel);
    this->State_GPU->AuxStateCPU[2 * this->State_GPU->NumberofNeuron + Target->index_in_NeuronModel] = total_current;
}

__global__ void EDLUTLikeLIF_GPU_Interface_Update(EDLUTLikeLIF_GPU** NeuralModelGPU, int currenttime, float timestepsize) {
    (*NeuralModelGPU)->UpdateState(currenttime, timestepsize);
}

void EDLUTLikeLIF_GPU_Interface::UpdateState(int index, int time, Simulation* simulation) {
    const long long total_start = bench_profile::now_ns();
    auto* stateGPU = this->State_GPU;
    int neuronNum = stateGPU->NumberofNeuron;
    int timedepInput = this->N_TimedependentInput;
    size_t copySize = sizeof(float) * neuronNum * timedepInput;
    size_t zeroSize = sizeof(float) * neuronNum * (timedepInput - 1);
    if (this->deviceProp.canMapHostMemory) {
        memset(this->State_GPU->InternalSpikeCPU, 0, sizeof(bool) * this->State_GPU->NumberofNeuron);
        const long long kernel_start = bench_profile::now_ns();
        EDLUTLikeLIF_GPU_Interface_Update<<<this->gridsize, this->blocksize, 0, this->computeStream>>>(
            this->NeuronModelOnGPU, time, simulation->basetimesteps);
        bench_profile::gpu_update_kernel_ns.fetch_add(bench_profile::now_ns() - kernel_start, std::memory_order_relaxed);
    } else {
        const long long h2d_start = bench_profile::now_ns();
        HANDLE_ERROR(cudaMemcpyAsync(stateGPU->AuxStateGPU, stateGPU->AuxStateCPU, copySize, cudaMemcpyHostToDevice, this->computeStream));
        bench_profile::gpu_update_memcpy_h2d_ns.fetch_add(bench_profile::now_ns() - h2d_start, std::memory_order_relaxed);
        HANDLE_ERROR(cudaMemsetAsync(this->State_GPU->InternalSpikeGPU, 0, sizeof(bool) * this->State_GPU->NumberofNeuron, this->computeStream));
        const long long kernel_start = bench_profile::now_ns();
        EDLUTLikeLIF_GPU_Interface_Update<<<this->gridsize, this->blocksize, 0, this->computeStream>>>(
            this->NeuronModelOnGPU, time, simulation->basetimesteps);
        bench_profile::gpu_update_kernel_ns.fetch_add(bench_profile::now_ns() - kernel_start, std::memory_order_relaxed);
        const long long d2h_start = bench_profile::now_ns();
        HANDLE_ERROR(cudaMemcpyAsync(
            this->State_GPU->InternalSpikeCPU,
            this->State_GPU->InternalSpikeGPU,
            sizeof(bool) * this->State_GPU->NumberofNeuron,
            cudaMemcpyDeviceToHost,
            this->computeStream));
        bench_profile::gpu_update_d2h_ns.fetch_add(bench_profile::now_ns() - d2h_start, std::memory_order_relaxed);
    }
    if (this->StateVector->IsMonitored) {
        const long long monitor_d2h_start = bench_profile::now_ns();
        HANDLE_ERROR(cudaMemcpyAsync(
            this->State_GPU->Vector_of_StateVariable,
            this->State_GPU->Vector_of_StateVariableGPU,
            this->State_GPU->NumberofNeuron * this->State_GPU->NumberofStateVariable * sizeof(float),
            cudaMemcpyDeviceToHost,
            this->computeStream));
        HANDLE_ERROR(cudaMemcpyAsync(this->State_GPU->LastUpdate, this->State_GPU->LastUpdateGPU, this->State_GPU->NumberofNeuron * sizeof(int), cudaMemcpyDeviceToHost, this->computeStream));
        HANDLE_ERROR(cudaMemcpyAsync(this->State_GPU->LastSpike, this->State_GPU->LastSpikingGPU, this->State_GPU->NumberofNeuron * sizeof(int), cudaMemcpyDeviceToHost, this->computeStream));
        bench_profile::gpu_update_d2h_ns.fetch_add(bench_profile::now_ns() - monitor_d2h_start, std::memory_order_relaxed);
    }
    const long long record_start = bench_profile::now_ns();
    HANDLE_ERROR(cudaEventRecord(this->sync_event, this->computeStream));
    bench_profile::gpu_update_event_record_ns.fetch_add(bench_profile::now_ns() - record_start, std::memory_order_relaxed);
    const long long sync_start = bench_profile::now_ns();
    HANDLE_ERROR(cudaEventSynchronize(this->sync_event));
    bench_profile::gpu_update_sync_ns.fetch_add(bench_profile::now_ns() - sync_start, std::memory_order_relaxed);
    const long long memset_start = bench_profile::now_ns();
    memset(stateGPU->AuxStateCPU, 0, zeroSize);
    bench_profile::gpu_update_memset_ns.fetch_add(bench_profile::now_ns() - memset_start, std::memory_order_relaxed);
    bench_profile::gpu_update_total_ns.fetch_add(bench_profile::now_ns() - total_start, std::memory_order_relaxed);
    bench_profile::gpu_update_count.fetch_add(1, std::memory_order_relaxed);
}

void EDLUTLikeLIF_GPU_Interface::InitStateVector(int NumberofNeuron, int GPUindex) {
    this->NumberOfNeuron = NumberofNeuron;
    this->GPU_ID = GPUindex % NumberOfGPU;
    HANDLE_ERROR(cudaSetDevice(GPUIndex[GPU_ID]));
    HANDLE_ERROR(cudaGetDeviceProperties(&this->deviceProp, GPUIndex[GPUindex % NumberOfGPU]));
    HANDLE_ERROR(cudaEventCreate(&this->sync_event));
    HANDLE_ERROR(cudaStreamCreate(&copyStream));
    HANDLE_ERROR(cudaStreamCreate(&computeStream));
    this->State_GPU = (Neuron_State_Vector_Interface*) this->StateVector;
    float new_init[] = { this->V_reset + this->init[0], 0.0f, 0.0f, 0.0f };
    float new_sigma[] = { this->sigma[0], this->sigma[1], this->sigma[2], this->sigma[3] };
    this->State_GPU->InitNeuronStateGPU(NumberofNeuron, new_init, new_sigma, this->N_TimedependentInput, this->deviceProp);
    this->InitializeClassGPU2(NumberofNeuron);
    InitializeVectorNeuronState_GPU2();
    this->CurrentSynapeModel = new CurrentSynapse(NumberofNeuron);
    InitializeInputCurrentSynapseStructure();
}

__global__ void EDLUTLikeLIF_GPU_C_Interface_InitializeClassGPU2(
    EDLUTLikeLIF_GPU** NeuralModelOnGPU,
    char* int_Name,
    float V_rest,
    float tau,
    float V_th,
    float R,
    float V_reset,
    int t_ref,
    float gexc_tau,
    float Eexc,
    float ginh_tau,
    float Einhibitory,
    int Timestep,
    float timesteps,
    void** d_parm,
    int N_neurons) {
    if (blockIdx.x == 0 && threadIdx.x == 0) {
        (*NeuralModelOnGPU) = new EDLUTLikeLIF_GPU(
            int_Name, V_rest, tau, V_th, R, V_reset, t_ref, gexc_tau, Eexc, ginh_tau, Einhibitory, Timestep, timesteps, d_parm, N_neurons);
    }
}

void EDLUTLikeLIF_GPU_Interface::InitializeClassGPU2(int NumberofNeuron) {
    HANDLE_ERROR(cudaMalloc(&(this->NeuronModelOnGPU), sizeof(EDLUTLikeLIF_GPU**)));
    std::vector<char> integrationNameHost(
        this->integrationMethodGPUInterface->name.begin(),
        this->integrationMethodGPUInterface->name.end());
    integrationNameHost.push_back('\0');
    char* integrationNameGPU = nullptr;
    HANDLE_ERROR(cudaMalloc((void**)&integrationNameGPU, integrationNameHost.size() * sizeof(char)));
    HANDLE_ERROR(cudaMemcpy(integrationNameGPU, integrationNameHost.data(), integrationNameHost.size() * sizeof(char), cudaMemcpyHostToDevice));
    this->blocksize = 128;
    this->gridsize = this->deviceProp.multiProcessorCount * 16;
    if ((NumberofNeuron + this->blocksize - 1) / this->blocksize < this->gridsize) {
        this->gridsize = (NumberofNeuron + this->blocksize - 1) / this->blocksize;
    }
    int TotalThread = this->gridsize * this->blocksize;
    this->integrationMethodGPUInterface->InitIntegrationMethodOnGPU(this->NumberOfNeuron, TotalThread);
    EDLUTLikeLIF_GPU_C_Interface_InitializeClassGPU2<<<1, 1>>>(
        this->NeuronModelOnGPU,
        integrationNameGPU,
        this->V_rest,
        this->tau,
        this->V_th,
        this->R,
        this->V_reset,
        this->t_ref,
        this->gexc_tau,
        this->Eexc,
        this->ginh_tau,
        this->Einhibitory,
        this->getTimestepSize(),
        this->timestepdouble,
        this->integrationMethodGPUInterface->d_param,
        this->NumberOfNeuron);
    HANDLE_ERROR(cudaGetLastError());
    HANDLE_ERROR(cudaDeviceSynchronize());
    HANDLE_ERROR(cudaFree(integrationNameGPU));
}

__global__ void initializeEDLUTLikeVectorNeuronState_GPU2(
    EDLUTLikeLIF_GPU** NeuralModelGPU,
    int NumberofVariable,
    float* InitState,
    float* AuxStateGPU,
    float* StateGPU,
    int* LastUpdateGPU,
    int* LastSpikeGPU,
    bool* InternalSpikeGPU,
    int NumberofNeurons) {
    if (blockIdx.x == 0 && threadIdx.x == 0) {
        (*NeuralModelGPU)->InitializeNeuronStateVector(
            NumberofVariable, InitState, AuxStateGPU, StateGPU, LastUpdateGPU, LastSpikeGPU, InternalSpikeGPU, NumberofNeurons);
    }
}

void EDLUTLikeLIF_GPU_Interface::InitializeVectorNeuronState_GPU2() {
    auto* State = (Neuron_State_Vector_Interface*) this->StateVector;
    initializeEDLUTLikeVectorNeuronState_GPU2<<<1, 1>>>(
        this->NeuronModelOnGPU,
        State->NumberofStateVariable,
        State->InitialStateGPU,
        State->AuxStateGPU,
        State->Vector_of_StateVariableGPU,
        State->LastUpdateGPU,
        State->LastSpikingGPU,
        State->InternalSpikeGPU,
        State->NumberofNeuron);
    HANDLE_ERROR(cudaGetLastError());
    HANDLE_ERROR(cudaDeviceSynchronize());
}

__global__ void SetEDLUTLikeSynapseOnGPU(EDLUTLikeLIF_GPU** NeuralModelGPU, bool Exc, bool Inh, bool I_EXT) {
    if (blockIdx.x == 0 && threadIdx.x == 0) {
        (*NeuralModelGPU)->SetEnabledSynapsis(Exc, Inh, I_EXT);
    }
}

void EDLUTLikeLIF_GPU_Interface::CheckType(Interconnections* inter) {
    int Type = inter->type;
    if (Type == 0 && this->Excited == false) {
        this->Excited = true;
        SetEDLUTLikeSynapseOnGPU<<<1, 1>>>(this->NeuronModelOnGPU, this->Excited, this->Inhibitory, this->I_EXT);
    } else if (Type == 1 && this->Inhibitory == false) {
        this->Inhibitory = true;
        SetEDLUTLikeSynapseOnGPU<<<1, 1>>>(this->NeuronModelOnGPU, this->Excited, this->Inhibitory, this->I_EXT);
    } else if (Type == 2) {
        if (this->Excited == false) {
            this->Excited = true;
        }
        SetEDLUTLikeSynapseOnGPU<<<1, 1>>>(this->NeuronModelOnGPU, this->Excited, this->Inhibitory, this->I_EXT);
    } else if (Type == 3) {
        this->I_EXT = true;
        inter->subindex_type = this->CurrentSynapeModel->N_connections[inter->TargetNeuron->index_in_NeuronModel];
        this->CurrentSynapeModel->IncrementNInputCurrentSynapsesPerNeuron(inter->TargetNeuron->index_in_NeuronModel);
        SetEDLUTLikeSynapseOnGPU<<<1, 1>>>(this->NeuronModelOnGPU, this->Excited, this->Inhibitory, this->I_EXT);
    } else if ((Type == 0 && this->Excited == true) || (Type == 1 && this->Inhibitory == true)) {
        return;
    } else {
        std::cout << "Error: Type of synapse is not supported in this model" << std::endl;
    }
}

void EDLUTLikeLIF_GPU_Interface::InitializeInputCurrentSynapseStructure() {
    if (this->CurrentSynapeModel != 0) {
        this->CurrentSynapeModel->InitializeInputCurrentPerSynapseStructure();
    }
}

void EDLUTLikeLIF_GPU_Interface::SetParameters(std::map<std::string, boost::any> parametermap, float basetimestep) {
    std::map<std::string, boost::any>::iterator iter = parametermap.find("V_rest");
    if (iter != parametermap.end()) { this->V_rest = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("tau");
    if (iter != parametermap.end()) { this->tau = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("V_th");
    if (iter != parametermap.end()) { this->V_th = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("R");
    if (iter != parametermap.end()) { this->R = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("V_reset");
    if (iter != parametermap.end()) { this->V_reset = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("t_ref");
    if (iter != parametermap.end()) { this->t_ref = boost::any_cast<int>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("gexc_tau");
    if (iter != parametermap.end()) { this->gexc_tau = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("Eexc");
    if (iter != parametermap.end()) { this->Eexc = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("ginh_tau");
    if (iter != parametermap.end()) { this->ginh_tau = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("Einh");
    if (iter != parametermap.end()) { this->Einhibitory = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("basetimestep");
    if (iter != parametermap.end()) { this->timestepdouble = boost::any_cast<float>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("random_mu");
    if (iter != parametermap.end()) { this->init = boost::any_cast<std::array<float, 4>>(iter->second); parametermap.erase(iter); }
    iter = parametermap.find("random_sigma");
    if (iter != parametermap.end()) { this->sigma = boost::any_cast<std::array<float, 4>>(iter->second); parametermap.erase(iter); }

    iter = parametermap.find("int_method");
    if (iter != parametermap.end()) {
        if (this->integrationMethodGPUInterface != 0) {
            delete this->integrationMethodGPUInterface;
        }
        ModelDescription temp = boost::any_cast<ModelDescription>(iter->second);
        temp.ModelParameter["step"] = basetimestep;
        this->integrationMethodGPUInterface = Integration_method_GPU_Interface_Factory<EDLUTLikeLIF_GPU_Interface>::create_Integration_method_GPU_Interface(temp, this);
        parametermap.erase(iter);
    } else {
        ModelDescription temp_modelDescription;
        temp_modelDescription.ModelName = "ForwardEulerMethod";
        temp_modelDescription.ModelParameter["step"] = basetimestep;
        this->integrationMethodGPUInterface = Integration_method_GPU_Interface_Factory<EDLUTLikeLIF_GPU_Interface>::create_Integration_method_GPU_Interface(temp_modelDescription, this);
    }
}

std::map<std::string, boost::any> EDLUTLikeLIF_GPU_Interface::getParameters() {
    std::map<std::string, boost::any> parametermap;
    parametermap["V_rest"] = this->V_rest;
    parametermap["tau"] = this->tau;
    parametermap["V_th"] = this->V_th;
    parametermap["R"] = this->R;
    parametermap["V_reset"] = this->V_reset;
    parametermap["t_ref"] = this->t_ref;
    parametermap["gexc_tau"] = this->gexc_tau;
    parametermap["Eexc"] = this->Eexc;
    parametermap["ginh_tau"] = this->ginh_tau;
    parametermap["Einh"] = this->Einhibitory;
    parametermap["basetimestep"] = this->timestepdouble;
    parametermap["random_mu"] = this->init;
    parametermap["random_sigma"] = this->sigma;
    ModelDescription temp;
    temp.ModelParameter = this->integrationMethodGPUInterface->getParameters();
    temp.ModelName = boost::any_cast<std::string>(temp.ModelParameter["name"]);
    parametermap["int_method"] = temp;
    return parametermap;
}
